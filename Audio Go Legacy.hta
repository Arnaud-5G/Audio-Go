<html lang="en">
<head>
    <title>Audio Go Legacy</title>

    <HTA:APPLICATION ID="OldAudioGo" APPLICATIONNAME="Audio Go Old UI" ICON="Data\ico\AppIcon.ico" BORDER="Dialog" CAPTION="Yes"
        SCROLL="No" SHOWINTASKBAR="Yes" SINGLEINSTANCE="Yes" SYSMENU="Yes" WINDOWSTATE="Normal" MaximizeButton="No">
</head>

<script language="VBScript">
    ' Create the needed objects
    Dim Shell : Set Shell = CreateObject("Shell.Application")
    ' if the dat files are missing replace them
    Set FSO = CreateObject("Scripting.FileSystemObject")
    ' Replace Bindings.dat
    If Not FSO.FileExists(".\Data\dat\Bindings.dat") Then 
        Set File = FSO.CreateTextFile(".\Data\dat\Bindings.dat", 2)
        File.Write "close 27" & vbCrLf & "pause 32" & vbCrLf & "loop 82" & vbCrLf & "minus 37" & vbCrLf & "plus 39" & vbCrLf & "up 38" & vbCrLf & "down 40"
    End If

    ' Resize the window
    Window.ResizeTo 392, 190 ' false x, true y

    'Create the Audio Player Object
    Dim oPlayer: Set oPlayer = CreateObject("WMPlayer.OCX")

    ' Create the vars
    started = false
    isPlaying = false
    isLooping = false
    pressedSpace = false
    pressedR = false
    startVolume = 50

    ' Bindings
    Key_close = CInt(Read(".\Data\dat\Bindings.dat", "close"))
    Key_pause = CInt(Read(".\Data\dat\Bindings.dat", "pause"))
    Key_loop = CInt(Read(".\Data\dat\Bindings.dat", "loop"))
    Key_minus = CInt(Read(".\Data\dat\Bindings.dat", "minus"))
    Key_plus = CInt(Read(".\Data\dat\Bindings.dat", "plus"))
    Key_up = CInt(Read(".\Data\dat\Bindings.dat", "up"))
    Key_down = CInt(Read(".\Data\dat\Bindings.dat", "down"))

    ' Pause States
    Const StatePause = ".\Data\png\PlayIcon.png"
    Const StatePlay = ".\Data\png\PauseIcon.png"

    ' Loop States
    Const StateNoLoop = ".\Data\png\NoLoopIcon.png"
    Const StateLoop = ".\Data\png\LoopIcon.png"

    ' Volume States
    Const StateMinusVolume = ".\Data\png\-VolumeIcon.png"
    Const StateMinVolume = ".\Data\png\-MinVolumeIcon.png"

    Const StatePlusVolume = ".\Data\png\+VolumeIcon.png"
    Const StateMaxVolume = ".\Data\png\+MaxVolumeIcon.png"

    ' is called when a song is chosen
    Sub StartUp
        SongPath = document.getElementById("mp3Selector").value
        If Not SongPath = "" Then
            started = true
            ' Play audio
            oPlayer.URL = SongPath
            oPlayer.settings.volume = startVolume

            If isLooping = True Then
                isLooping = False
                ToggleLoop
            End If
        End If
    End Sub

    ' to read in memory
    Function Read(Path, Var)
        Set FSO = CreateObject("Scripting.FileSystemObject")
        Set File = FSO.OpenTextFile(Path, 1) ' 1 = reading, 2 = write, 8 = write at the end

        allData = File.readall
        File.Close
        For Each ln In Split(allData, vbCrLf)
            VarName = Split(ln, " ", 2)(0)
            If VarName = Var Then
                output = Split(ln, " ", 2)(1)
                Exit For
            End If
        Next

        ' memory management (vbs does not have one for objects only vars have one)
        Set File = Nothing
        Set FSO = Nothing

        ' return
        Read = output
    End Function

    Function GetLastVar(Path)
        Set FSO = CreateObject("Scripting.FileSystemObject")
        Set File = FSO.OpenTextFile(Path, 1) ' 1 = reading, 2 = write, 8 = write at the end

        If FSO.GetFile(Path).Size = 0 Then ' if the file is empty
            GetLastVar = ""
            Exit Function
        End If

        allData = File.readall
        File.Close

        If Split(allData, vbCrLf)(UBound(Split(allData, vbCrLf))-1) = "" Then ' if last line is empty
            GetLastVar = ""
            Exit Function
        End If

        VarName = Split(Split(allData, vbCrLf)(UBound(Split(allData, vbCrLf))-1), " ", 2)(0) ' get the last var (UBound = last index)
        ' memory management (vbs does not have one for objects only vars have one)
        Set File = Nothing
        Set FSO = Nothing

        ' return
        GetLastVar = VarName
    End Function

    ' to write in memory
    Sub WriteVar(Path, Var, Text)
        Set FSO = CreateObject("Scripting.FileSystemObject")
        Set File = FSO.OpenTextFile(Path, 1) ' 1 = reading, 2 = write, 8 = write at the end
        PathOver = Split(Path, "\")
        i = -1
        For each x in PathOver
            i = i+1
        Next
        
        PathOverwrite = ""
        y = -1

        For each x in PathOver
            y = y+1
            If Not y = i Then
                PathOverwrite = PathOverwrite & x & "\"
            Else
                PathOverwrite = PathOverwrite & "temp.dat"
            End If
        Next

        Set FileOver = FSO.CreateTextFile(PathOverwrite, 2) ' 1 = reading, 2 = write, 8 = write at the end
        
        Do While Not File.AtEndofStream
            ln = File.ReadLine
            If ln <> "" Then
                VarName = Split(ln, " ", 2)(0)
            End If

            If VarName = Var Then
                FileOver.WriteLine Var & " " & Text
                added = true
            Else
                FileOver.WriteLine ln
            End If
        Loop

        If added <> true Then
            FileOver.WriteLine Var & " " & Text
        End If

        File.Close
        FileOver.Close

        Set FileOver = FSO.OpenTextFile(PathOverwrite, 1)
        Set File = FSO.OpenTextFile(Path, 2)

        File.Write ""
        If FSO.GetFile(PathOverwrite).Size <> 0 Then
            text = FileOver.ReadAll
            File.Write text
        End If
        File.Close
        FileOver.Close

        FSO.DeleteFile(PathOverwrite)
    End Sub

    Sub mp3Selector_OnClick
        started = false
        oPlayer.controls.pause
        isPlaying = false

        document.getElementById("PauseImg").src = StatePause
    End Sub

    Sub TogglePause
        If isPlaying = false Then If pressedSpace = false Then Play()
        If isPlaying = true Then If pressedSpace = false Then Pause()
        pressedSpace = false
    End Sub

    ' Called each time a key is pressed
    Sub document_OnKeyDown
        If Window.event.keyCode = Key_close Then Window.close()
        If Window.event.keyCode = Key_pause Then TogglePause()
        If Window.event.keyCode = Key_loop Then If started = true Then ToggleLoop()
        If Window.event.keyCode = Key_minus Then If started = true Then MinusButton_OnClick()
        If Window.event.keyCode = Key_plus Then If started = true Then PlusButton_OnClick()
        If Window.event.keyCode = Key_up Then If started = true Then VolumeUpButton_OnClick()
        If Window.event.keyCode = Key_down Then If started = true Then VolumeDownButton_OnClick()
    End Sub

    Sub PauseButton_OnClick
        If started = true Then TogglePause()
        If started = false Then If(document.getElementById("mp3Selector").value <> "") = true Then TogglePause()
    End Sub
    
    Sub Play
        If started = false Then StartUp()
        document.getElementById("PauseImg").src = StatePlay
        oPlayer.controls.play
        isPlaying = true
        pressedSpace = true
    End Sub

    Sub Pause
        document.getElementById("PauseImg").src = StatePause
        oPlayer.controls.pause
        isPlaying = false
        pressedSpace = true
    End Sub

    Sub RebindButton_OnClick
        Dim arr : arr = Window.showModalDialog(".\Data\hta\OldUI\Rebind.hta", "", "dialogHeight: 228px; dialogWidth: 190px;")
        If Not arr(0) = "" Then Key_close = arr(0)
        If Not arr(1) = "" Then Key_pause = arr(1)
        If Not arr(2) = "" Then Key_loop = arr(2)
        If Not arr(3) = "" Then Key_minus = arr(3)
        If Not arr(4) = "" Then Key_plus = arr(4)
        If Not arr(5) = "" Then Key_up = arr(5)
        If Not arr(6) = "" Then Key_down = arr(6)
    End Sub

    Sub MinusButton_OnClick
        oPlayer.controls.currentPosition = oPlayer.controls.currentPosition - 5
    End Sub

    Sub PlusButton_OnClick
        oPlayer.controls.currentPosition = oPlayer.controls.currentPosition + 5
    End Sub

    Sub VolumeDownButton_OnClick
        If oPlayer.settings.volume < 6 Then
            document.getElementById("DownImg").src = StateMinVolume
        Else 
            document.getElementById("UpImg").src = StatePlusVolume
        End If

        oPlayer.settings.volume = oPlayer.settings.volume - 5
        startVolume = startVolume - 5
    End Sub

    Sub VolumeUpButton_OnClick
        If oPlayer.settings.volume > 94 Then
            document.getElementById("UpImg").src = StateMaxVolume
        Else 
            document.getElementById("DownImg").src = StateMinusVolume
        End If

        oPlayer.settings.volume = oPlayer.settings.volume + 5
        startVolume = startVolume + 5
    End Sub

    Sub LoopButton_OnClick
        ToggleLoop()
    End Sub

    Sub ToggleLoop
        If isLooping = true Then If pressedR = false Then setLoop(False)
        If isLooping = false Then If pressedR = false Then setLoop(True)
        pressedR = false
    End Sub

    Sub setLoop(Bool)
        oPlayer.settings.setMode "loop", Bool
        pressedR = true
        If isLooping = true Then document.getElementById("LoopImg").src = StateNoLoop
        If isLooping = false Then document.getElementById("LoopImg").src = StateLoop
        isLooping = Bool
    End Sub

    Sub Foo
    End Sub

</script>

<body>
    <!-- First Layer -->
    <button type="button" id="RebindButton" style="height: 16px; width: 16px;" name="RebindButton" onclick="focus(Window)" tabindex="-1"></button>
    
    <br>

    <!-- Second Layer -->
    <input type="file" id="mp3Selector" name="mp3Selector" value="" onclick="focus(Window)" tabindex="-1">

    <!-- Third Layer -->
    <button type="button" id="MinusButton" style="margin: 0px;" name="MinusButton" onclick="focus(Window)" tabindex="-1">
        <img src="Data/png/-TimeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="PauseButton" style="margin: 0px 6px 0px;" name="PauseButton" onclick="focus(Window)" tabindex="-1">
        <img id="PauseImg" src="Data/png/PlayIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="PlusButton" style="margin: 0px;" name="PlusButton" onclick="focus(Window)" tabindex="-1">
        <img src="Data/png/+TimeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="VolumeDownButton" style="margin: 0px 6px 0px;" name="VolumeDownButton" onclick="focus(Window)" tabindex="-1">
        <img id="DownImg" src="Data/png/-VolumeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="VolumeUpButton" style="margin: 0px;" name="VolumeUpButton" onclick="focus(Window)" tabindex="-1">
        <img id="UpImg" src="Data/png/+VolumeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="LoopButton" style="margin: 0px 17px 0px;" name="LoopButton" onclick="focus(Window)" tabindex="-1">
        <img id="LoopImg" src="Data/png/NoLoopIcon.png" height="32px" width="32px" /></button>
</body>