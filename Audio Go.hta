<html lang="en">
    
<head>
    <title>Audio Go</title>
    <HTA:APPLICATION ID="oHTA" APPLICATIONNAME="Audio Go" ICON="ico\AppIcon.ico" BORDER="Dialog" CAPTION="Yes"
        SCROLL="No" SHOWINTASKBAR="Yes" SINGLEINSTANCE="No" SYSMENU="Yes" WINDOWSTATE="normal" MaximizeButton="No">
</head>

<script language="VBScript">
    ' Resize the window
    Window.ResizeTo 392, 190 ' false x, true y

    'Create the Audio Player Object
    Dim oPlayer: Set oPlayer = CreateObject("WMPlayer.OCX")

    ' Create the vars
    started = false
    isPlaying = false
    isLooping = false
    pressedSpace = false
    pressedR = false
    startVolume = 50

    ' Bindings
    Key_close = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "close")
    Key_pause = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "pause")
    Key_loop = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "loop")
    Key_minus = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "minus")
    Key_plus = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "plus")
    Key_up = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "up")
    Key_down = Read("C:\Users\20200791\Desktop\Repos\Audio-Go\dat\Bindings.dat", "down")

    ' Pause States
    StatePause = "png\PlayIcon.png"
    StatePlay = "png\PauseIcon.png"

    ' Loop States
    StateNoLoop = "png\NoLoopIcon.png"
    StateLoop = "png\LoopIcon.png"

    ' Volume States
    StateMinusVolume = "png\-VolumeIcon.png"
    StateMinVolume = "png\-MinVolumeIcon.png"

    StatePlusVolume = "png\+VolumeIcon.png"
    StateMaxVolume = "png\+MaxVolumeIcon.png"

    ' is called when a song is chosen
    Sub StartUp
        SongPath = document.getElementById("mp3Selector").value
        started = true
        ' Play audio
        oPlayer.URL = SongPath
        oPlayer.settings.volume = startVolume

        ' ex. oPlayer.Controls.currentPosition = 0 ? test = oPlayer.Controls.currentPosition
        ' msgbox oPlayer.currentmedia.duration

        a = Split(SongPath, "\", -1)
        i = -1
        for each x in a
            i = i + 1
        next
        b = Split(a(i), ".", 1, 0)
        SongName = Split(b(0), ".")(0)
        document.getElementById("mp3Selector").value = SongName
    End Sub

    ' to read in memory
    Function Read(Path, Var)
        Set FSO = CreateObject("Scripting.FileSystemObject")
        Set File = FSO.OpenTextFile(Path, 1) ' 1 = reading, 2 = write, 8 = write at the end

        allData = File.readall
        File.Close
        For Each ln In Split(allData, vbCrLf)
            VarName = Split(ln, " ", 2)(0)
            If VarName = Var Then
                output = Split(ln, " ", 2)(1)
                Exit For
            End If
        Next

        ' memory management (vbs does not have one for objects only vars have one)
        Set File = Nothing
        Set FSO = Nothing

        ' return
        Read = CInt(output)
    End Function

    ' to write in memory
    Function Write(Path, Var, Text)
        Set FSO = CreateObject("Scripting.FileSystemObject")
        Set File = FSO.OpenTextFile(Path, 1) ' 1 = reading, 2 = write, 8 = write at the end
        PathOver = Split(Path, "\")
        i = -1
        For each x in PathOver
            i = i+1
        Next
        
        PathOverwrite = ""
        y = -1

        For each x in PathOver
            y = y+1
            If Not y = i Then
                PathOverwrite = PathOverwrite & x & "\"
            Else
                PathOverwrite = PathOverwrite & "temp.dat"
            End If
        Next

        Set FileOver = FSO.CreateTextFile(PathOverwrite, 2) ' 1 = reading, 2 = write, 8 = write at the end
        
        Do While Not File.AtEndofStream
            ln = File.ReadLine
            VarName = Split(ln, " ", 2)(0)
            If VarName = Var Then
                FileOver.WriteLine Var & " " & Text
            Else
                FileOver.WriteLine ln
            End If
        Loop

        File.Close
        FileOver.Close

        Set FileOver = FSO.OpenTextFile(PathOverwrite, 1)
        Set File = FSO.OpenTextFile(Path, 2)

        File.Write ""
        text = FileOver.ReadAll
        File.Write text
        File.Close
        FileOver.Close

        FSO.DeleteFile(PathOverwrite)
    End Function

    Sub mp3Selector_OnClick
        started = false
        oPlayer.controls.pause
        isPlaying = false
        isLooping = false

        document.getElementById("PauseImg").src = StatePause
        document.getElementById("LoopImg").src = StateNoLoop
    End Sub

    Sub TogglePause
        If isPlaying = false Then If pressedSpace = false Then Play()
        If isPlaying = true Then If pressedSpace = false Then Pause()
        pressedSpace = false
    End Sub

    ' Called each time a key is pressed
    Sub document_OnKeyDown
        lnum = Window.event.keyCode
        If Window.event.keyCode = Key_close Then Window.close()
        If Window.event.keyCode = Key_pause Then TogglePause()
        If Window.event.keyCode = Key_loop Then If started = true Then ToggleLoop()
        If Window.event.keyCode = Key_minus Then If started = true Then MinusButton_OnClick()
        If Window.event.keyCode = Key_plus Then If started = true Then PlusButton_OnClick()
        If Window.event.keyCode = Key_up Then If started = true Then VolumeUpButton_OnClick()
        If Window.event.keyCode = Key_down Then If started = true Then VolumeDownButton_OnClick()
    End Sub

    Sub PauseButton_OnClick
        If started = true Then TogglePause()
        If started = false Then If(document.getElementById("mp3Selector").value <> "") = true Then TogglePause()
    End Sub
    
    Sub Play
        If started = false Then StartUp()
        document.getElementById("PauseImg").src = StatePlay
        oPlayer.controls.play
        isPlaying = true
        pressedSpace = true
    End Sub

    Sub Pause
        document.getElementById("PauseImg").src = StatePause
        oPlayer.controls.pause
        isPlaying = false
        pressedSpace = true
    End Sub

    Sub RebindButton_OnClick
        Dim arr : arr = Window.showModalDialog("hta\Rebind.hta", "", "dialogHeight: 228px; dialogWidth: 300px;")
        Key_close = arr(0)
        Key_pause = arr(1)
        Key_loop = arr(2)
        Key_minus = arr(3)
        Key_plus = arr(4)
        Key_up = arr(5)
        Key_down = arr(6)
    End Sub

    Sub MinusButton_OnClick
        oPlayer.controls.currentPosition = oPlayer.controls.currentPosition - 5
    End Sub

    Sub PlusButton_OnClick
        oPlayer.controls.currentPosition = oPlayer.controls.currentPosition + 5
    End Sub

    Sub VolumeDownButton_OnClick
        If oPlayer.settings.volume < 6 Then
            document.getElementById("DownImg").src = StateMinVolume
        Else 
            document.getElementById("UpImg").src = StatePlusVolume
        End If

        oPlayer.settings.volume = oPlayer.settings.volume - 5
        startVolume = startVolume - 5
    End Sub

    Sub VolumeUpButton_OnClick
        If oPlayer.settings.volume > 94 Then
            document.getElementById("UpImg").src = StateMaxVolume
        Else 
            document.getElementById("DownImg").src = StateMinusVolume
        End If

        oPlayer.settings.volume = oPlayer.settings.volume + 5
        startVolume = startVolume + 5
    End Sub

    Sub LoopButton_OnClick
        If started = true Then ToggleLoop()
    End Sub

    Sub ToggleLoop
        If isLooping = true Then If pressedR = false Then setLoop(False)
        If isLooping = false Then If pressedR = false Then setLoop(True)
        pressedR = false
    End Sub

    Sub setLoop(Bool)
        oPlayer.settings.setMode "loop", Bool
        pressedR = true
        If isLooping = true Then document.getElementById("LoopImg").src = StateNoLoop
        If isLooping = false Then document.getElementById("LoopImg").src = StateLoop
        isLooping = Bool
    End Sub

    Sub Foo
    End Sub

</script>

<body>
    <!-- First Layer -->
    <button type="button" id="RebindButton" style="height: 16px; width: 16px;" name="RebindButton" onclick="focus(Window)" tabindex="-1"></button>

    <br>

    <!-- Second Layer -->
    <input type="file" id="mp3Selector" name="mp3Selector" value="" onclick="focus(Window)" tabindex="-1">

    <br>

    <!-- Third Layer -->
    <button type="button" id="MinusButton" style="margin: 0px;" name="MinusButton" onclick="focus(Window)" tabindex="-1">
        <img src="png/-TimeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="PauseButton" style="margin: 0px 6px 0px;" name="PauseButton" onclick="focus(Window)" tabindex="-1">
        <img id="PauseImg" src="png/PlayIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="PlusButton" style="margin: 0px;" name="PlusButton" onclick="focus(Window)" tabindex="-1">
        <img src="png\+TimeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="VolumeDownButton" style="margin: 0px 6px 0px;" name="VolumeDownButton" onclick="focus(Window)" tabindex="-1">
        <img id="DownImg" src="png\-VolumeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="VolumeUpButton" style="margin: 0px;" name="VolumeUpButton" onclick="focus(Window)" tabindex="-1">
        <img id="UpImg" src="png\+VolumeIcon.png" height="32px" width="32px" /></button>
    <button type="button" id="LoopButton" style="margin: 0px 17px 0px;" name="LoopButton" onclick="focus(Window)" tabindex="-1">
        <img id="LoopImg" src="png\NoLoopIcon.png" height="32px" width="32px" /></button>
</body>
